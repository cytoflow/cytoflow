# script defs

!define APP_NAME "Cytoflow"
!define INSTDIR_REG_ROOT "HKCU"
!define INSTDIR_REG_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APP_NAME}"
!define DESCRIPTION "Better quantitative flow cytometry"
# These three must be integers
!define VERSIONMAJOR 1
!define VERSIONMINOR 1
!define VERSIONBUILD 0
# These will be displayed by the "Click here for support information" link in "Add/Remove Programs"
# It is possible to use "mailto:" links in here to open the email client
!define HELPURL "https://github.com/cytoflow/cytoflow/issues" # "Support Information" link
!define UPDATEURL "http://cytoflow.github.io/" # "Product Updates" link
!define ABOUTURL "https://teague.bio" # "Publisher" link
# This is the size (in kB) of all the files copied into "Program Files"
!define INSTALLSIZE 1003520

# install for current user only
!define MULTIUSER_INSTALLMODE_INSTDIR "${APP_NAME}"

!define MULTIUSER_EXECUTIONLEVEL Standard
!define MULTIUSER_MUI 1
#!define MULTIUSER_INSTALLMODE_COMMANDLINE 1

!include MultiUser.nsh
 
!include MUI2.nsh
 
!include "package\AdvUninstLog.nsh"
 
Name "${APP_NAME}"
Icon "package\icon.ico"
OutFile "dist\cytoflow-installer.exe"
ShowInstDetails show
ShowUninstDetails show
#InstallDir "$PROGRAMFILES\${APP_NAME}"
InstallDirRegKey ${INSTDIR_REG_ROOT} "${INSTDIR_REG_KEY}" "InstallDir"
 
!insertmacro UNATTENDED_UNINSTALL
 
!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE "LICENSE.txt"
#!insertmacro MULTIUSER_PAGE_INSTALLMODE
#!insertmacro MUI_PAGE_COMPONENTS
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES
!insertmacro MUI_PAGE_FINISH
 
!insertmacro MUI_UNPAGE_WELCOME
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_UNPAGE_FINISH
 
!insertmacro MUI_LANGUAGE "English"
 
Section "Main Application" sec01
	SetOutPath '$INSTDIR'
 
	!insertmacro UNINSTALL.LOG_OPEN_INSTALL
 
	File /r "dist\cytoflow\*"
 
	!insertmacro UNINSTALL.LOG_CLOSE_INSTALL
 
	CreateDirectory '$SMPROGRAMS\${APP_NAME}'
        CreateShortcut '$SMPROGRAMS\${APP_NAME}\Cytoflow.lnk' '$INSTDIR\cytoflow.exe'
        CreateShortcut '$SMPROGRAMS\${APP_NAME}\Uninstall.lnk' '${UNINST_EXE}'


	WriteRegStr ${INSTDIR_REG_ROOT} "${INSTDIR_REG_KEY}" "DisplayName" "${APP_NAME} - ${DESCRIPTION}"
	WriteRegStr ${INSTDIR_REG_ROOT} "${INSTDIR_REG_KEY}" "UninstallString" "$\"$INSTDIR\uninstall.exe$\""
# 	WriteRegStr ${INSTDIR_REG_ROOT} "${INSTDIR_REG_KEY}" "QuietUninstallString" "$\"$INSTDIR\uninstall.exe$\" /S"
 	WriteRegStr ${INSTDIR_REG_ROOT} "${INSTDIR_REG_KEY}" "InstallLocation" "$\"$INSTDIR$\""
 	WriteRegStr ${INSTDIR_REG_ROOT} "${INSTDIR_REG_KEY}" "DisplayIcon" "$\"$INSTDIR\logo.ico$\""
 	WriteRegStr ${INSTDIR_REG_ROOT} "${INSTDIR_REG_KEY}" "Publisher" "${APP_NAME}"
 	WriteRegStr ${INSTDIR_REG_ROOT} "${INSTDIR_REG_KEY}" "HelpLink" "${HELPURL}"
 	WriteRegStr ${INSTDIR_REG_ROOT} "${INSTDIR_REG_KEY}" "URLUpdateInfo" "${UPDATEURL}"
 	WriteRegStr ${INSTDIR_REG_ROOT} "${INSTDIR_REG_KEY}" "URLInfoAbout" "${ABOUTURL}"
 	WriteRegStr ${INSTDIR_REG_ROOT} "${INSTDIR_REG_KEY}" "DisplayVersion" "${VERSIONMAJOR}.${VERSIONMINOR}.${VERSIONBUILD}"
 	WriteRegStr ${INSTDIR_REG_ROOT} "${INSTDIR_REG_KEY}" "VersionMajor" ${VERSIONMAJOR}
 	WriteRegStr ${INSTDIR_REG_ROOT} "${INSTDIR_REG_KEY}" "VersionMinor" ${VERSIONMINOR}
	# There is no option for modifying or repairing the install
	WriteRegDWORD ${INSTDIR_REG_ROOT} "${INSTDIR_REG_KEY}" "NoModify" 1
	WriteRegDWORD ${INSTDIR_REG_ROOT} "${INSTDIR_REG_KEY}" "NoRepair" 1
	# Set the INSTALLSIZE constant (!defined at the top of this script) so Add/Remove Programs can accurately report the size
	WriteRegDWORD ${INSTDIR_REG_ROOT} "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APP_NAME}" "EstimatedSize" ${INSTALLSIZE}

 
	#WriteRegStr ${INSTDIR_REG_ROOT} "${INSTDIR_REG_KEY}" "InstallDir" "$INSTDIR"
	#WriteRegStr ${INSTDIR_REG_ROOT} "${INSTDIR_REG_KEY}" "DisplayName" "${APP_NAME}"
	#WriteRegStr ${INSTDIR_REG_ROOT} "${INSTDIR_REG_KEY}" "UninstallString" "${UNINST_EXE}"
SectionEnd
 
Function .onInit
	!insertmacro MULTIUSER_INIT
	!insertmacro UNINSTALL.LOG_PREPARE_INSTALL
FunctionEnd
 
Function .onInstSuccess
	!insertmacro UNINSTALL.LOG_UPDATE_INSTALL
FunctionEnd
 
Section UnInstall
	!insertmacro UNINSTALL.LOG_UNINSTALL "$INSTDIR"
	!insertmacro UNINSTALL.LOG_UNINSTALL "$APPDATA\${APP_NAME}"
	!insertmacro UNINSTALL.LOG_END_UNINSTALL


        Delete "$SMPROGRAMS\${APP_NAME}\Cytoflow.lnk"
        Delete "$SMPROGRAMS\${APP_NAME}\Uninstall.lnk"
        RmDir "$SMPROGRAMS\${APP_NAME}"
 
	DeleteRegKey ${INSTDIR_REG_ROOT} "${INSTDIR_REG_KEY}"
SectionEnd
 
Function UN.onInit
        !insertmacro MULTIUSER_UNINIT
	!insertmacro UNINSTALL.LOG_BEGIN_UNINSTALL
FunctionEnd
